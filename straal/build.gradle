plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}
android {
    compileSdkVersion 30
    resourcePrefix "straal"

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 30
        versionCode 6
        versionName "1.0.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            straalSdk(MavenPublication) {
                groupId = 'com.straal.android.sdk'
                artifactId = 'StraalSdk'
                from components.release

                pom {
                    name = "StraalSdk"
                    description = "Straal helper library for mobile payments"
                    url = "https://straal.com"

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/straal/straal-android.git'
                        developerConnection = 'scm:git:ssh://github.com/straal/straal-android.git'
                        url = 'https://github.com/straal/straal-android'
                    }
                }
            }
        }

        repositories {
            maven {
                name = "sonatype"
                url = "sonatype url"
                credentials {
                    username "username"
                    password "password"
                }
            }
        }

        signing {
            useGpgCmd() //https://github.com/gradle/gradle/issues/8657
            sign publishing.publications.straalSdk
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.annotation:annotation:1.1.0"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.activity:activity:1.2.0'
    implementation 'androidx.fragment:fragment:1.3.0'
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.4.0"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.4.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.4.0"
    testImplementation "org.json:json:20171018"
    testImplementation 'org.mockito:mockito-core:3.0.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:core:1.3.0'
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.3.0"
}
